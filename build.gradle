plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation ('org.uncommons.maths:uncommons-maths:1.2.2') {
        exclude group: 'jfree'
    }
}

sourceSets {
    main {
         java {
            srcDirs = ['src']
         }
    }
}

jar {
    manifest {
        attributes("Main-Class": "info.spotcomms.extirpater.Start")
    }
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

def getVersionGit() {
    def versionGit = ""
    try {
        def gitAvailable = "command -v git".execute()
        gitAvailable.waitFor()
        if(gitAvailable.exitValue() == 0) {
            def commit = "git rev-parse --short HEAD".execute().text.trim()
            def commitDate = "git show -s --format=%cs ".concat(commit).execute().text.trim()
            versionGit = "_" + commitDate + '_' + commit
        } else {
            println("git is unavailable")
        }
    } catch (Exception e) {
        println("unable to check for git")
    }
    return versionGit
}
version = '2.9.1' + getVersionGit()
